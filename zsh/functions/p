# vim: filetype=zsh foldmethod=marker
# helper function for publishing content.

# Init {{{
local PUBPRG=pandoc
local SRCFMT="md"
local PUBFMT="html"
local SRCDIR="$HOME/org"
local PUBDIR="$SRCDIR/docs"
local FILESRX='^\d+-\d+-\d+--\w+--\w+'
local SRCFILES="/tmp/src.txt"
local PUBFILES="/tmp/pub.txt"
local SRCNAME
local TEMPLATE
# }}}

# get_template {{{
  emulate -L zsh
__get_template() {
  # taken from https://github.com/jgm/pandoc/issues/1958#issuecomment-81978951
  # extract the template from the SCRFILE else use default template
  TEMPLATE=$(sed -n 's/^template:[ ][ ]*\(.*\)/\1/p' $SRCDIR/$SRCNAME.$SRCFMT | sed 's/^\(..*\)$/\1/')
}
# }}}

# sync {{{
__sync() {
  emulate -L zsh
  fd --extension $SRCFMT $FILESRX $SRCDIR --exec echo {/.} | sort > $SRCFILES
  fd --extension $PUBFMT $FILESRX $PUBDIR --exec echo {/.} | sort > $PUBFILES
}
# }}}

# ls {{{
__ls() {
  emulate -L zsh

  if [[ -n "$1" ]]; then
    local NAME="${1:t:r}"
    if [[ -e "$PUBDIR/$NAME.$PUBFMT" ]]; then
      echo "$1 is published."
    else
      echo "$1 is unpublished."
    fi
  else
    __sync
    # print all published files
    comm -12 $SRCFILES $PUBFILES | column
  fi
}
# }}}

# all {{{
__all() {
  emulate -L zsh

  __sync
  echo -n "Info: publish $(cat $PUBFILES | wc -l) files? [y/n]: "
  read ANS
  [[ "${ANS:l}" != "y" ]] && return 1

  for SRCNAME in $(comm -12 $SRCFILES $PUBFILES); do # published files
    __publish
  done
}
# }}}

# browse {{{
__browse() {
  emulate -L zsh

  if [[ -n "$1" ]]; then
    local NAME="${1:t:r}"
    if [[ -e "$PUBDIR/$NAME.$PUBFMT" ]]; then
      open "https://arumoy.me/org/$NAME"
    else
      echo "Info: $1 is not published."
      return 1
    fi
  else
    open 'https://arumoy.me/org'
  fi
}
# }}}

# __publish {{{
__publish() {
  __get_template
  if [[ -z "$TEMPLATE" ]]; then
    echo "Error: $SRCNAME.$SRCFMT does not specify a template."
    echo "To publish, specify a template in the header."
    return 1
  fi

  echo "Info: publishing $SRCNAME.$SRCFMT as $PUBDIR/$SRCNAME.$PUBFMT with template $TEMPLATE"
  $PUBPRG ${@:2} --template=$TEMPLATE --output $PUBDIR/$SRCNAME.$PUBFMT $SRCNAME.$SRCFMT
}
# }}}

# Main {{{
case "$1" in
  ls )
    __ls "${@:2}"
    ;;
  browse )
    __browse "${@:2}"
    ;;
  all )
    __all "${@:2}"
    ;;
  * )
    if [[ -e "$SRCDIR/$1" ]]; then
      SRCNAME="${1:t:r}" # only the name
      __publish
    fi
    ;;
esac
# }}}

